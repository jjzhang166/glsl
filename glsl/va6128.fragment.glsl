
#ifdef GL_ES
precision mediump float;
#endif

// T21: Can anyone fix the fringe around the cylinders ???
// 
// fernlightning says: Lets try adding a ring/disk at the end of the cylinder, and ensuring the color returned is stable

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

float rand(vec3 n)
{
  n = floor(n);
  return fract(sin((n.x+n.y*1e2+n.z*1e4)*1e-4)*1e5);
}

// .x is distance, .y = colour
vec2 map( vec3 p )
{
	const float RADIUS = 0.2;

	
	p.z += sin(floor(p.x*4.0) + time)*5.0;
	p.z *= (1.0 + sin(floor(p.y*4.0) + time*0.1)*0.2);
	
	// cylinder
	vec3 f = fract( p ) - 0.5;
	float d = length( f.xy );
        float cr = rand( p );
	float cd = d - cr*RADIUS;

        // end - calc (rand) radius at more stable pos
	p.z -= 0.5;
	float rr = rand( p );
	float rn = d - rr*RADIUS;
        float rm = abs( fract( p.z ) - 0.5 );  // offset so at end of cylinder
        
	// float rd = max( rn , rm ); // end with disk
	float rd = sqrt( rn*rn + rm*rm ); // end with ring


	rd=pow(rd,01.5);// Flange mod - break the dist ambiguity fix tho :(
	return (cd < rd) ?  vec2( cd, cr ) : vec2( rd, rr ); // min
}


void main( void )
{
    vec2 pos = (gl_FragCoord.xy*2.0 - resolution.xy) / resolution.y;
    vec3 camPos = vec3(cos(time*0.3), sin(time*0.3), 3.5);
    vec3 camTarget = vec3(0.0, 0.0, .0);

    vec3 camDir = normalize(camTarget-camPos);
    vec3 camUp  = normalize(vec3(0.0, 1.0, 0.0));
    vec3 camSide = cross(camDir, camUp);
    float focus = 1.8;

    vec3 rayDir = normalize(camSide*pos.x + camUp*pos.y + camDir*focus);
    vec3 ray = camPos;
    float m = 0.32;
    vec2 d;
    float total_d = 0.;
    const int MAX_MARCH = 100;
    const float MAX_DISTANCE = 100.0;
    for(int i=0; i<MAX_MARCH; ++i) {
        d = map(ray-vec3(0.,0.,time/2.));
        total_d += d.x;
        ray += rayDir * d.x;
        m += 1.0;
        if(abs(d.x)<0.01) { break; }
        if(total_d>MAX_DISTANCE) { total_d=MAX_DISTANCE; break; }
    }

    float c = (total_d)*0.0001;
    vec4 result = vec4( 1.0-vec3(c, c, c) - vec3(0.025, 0.025, 0.02)*m*0.8, 1.0 );
    gl_FragColor = result*d.y;
}
